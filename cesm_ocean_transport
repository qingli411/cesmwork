#! /bin/csh
# 
# This script get transport variables from annual mean POP output
#
# Li Qing, 20150713
#

if ( $#argv < 3 || $#argv > 4 ) then
cat << EOF
USAGE:
	cesm_ocean_transport [case name] [start year] [end year] [data path]
		data path = "pwork": data saved in /glade/p/work
		data path = "scratch": data saved in /glade/scratch
EOF
exit -1;
endif

	set casename	= $1
	@ yrini = $2
	@ yrend = $3
	if ( $#argv == 4 ) then
		set dptype	= $4
		if ( ${dptype} == "pwork" ) then
			set datapath	= /glade/p/work/qingli/archive/${casename}/ocn/hist
		else if ( ${dptype} == "scratch" ) then
			set datapath	= /glade/scratch/qingli/archive/${casename}/ocn/hist
		else
			echo "cesm_avg_ocean: error in data path type..."
			exit -1
		endif
	else if ( $#argv == 3) then
		set datapath	= /glade/scratch/qingli/archive/${casename}/ocn/hist
	endif

	set datato	= /glade/p/work/qingli/data/${casename}

	set var = TRANS
	set vars = "MOC,N_HEAT,N_SALT,moc_components,transport_components,transport_regions,moc_z,lat_aux_grid,time"

	set prefixi	= ${casename}.pop.h
	set prefixo	= ${casename}.pop.h
	set workdir	= /glade/scratch/qingli/tmp
	set tmpdir	= $$
	set yrini_str	= `printf %04d $yrini`
	set yrend_str	= `printf %04d $yrend`
	set outfile 	= ${prefixo}.${var}.${yrini_str}.${yrend_str}.ann

	echo "- STARTED -"
	echo "Input Directory: $datapath"
	echo "Output Directory: $datato"
	
	cd $workdir
	mkdir ${tmpdir}
	cd ${tmpdir}

	echo " Extracting Variable: $var ..."   
	set yr = $yrini
	while ( $yr <= $yrend )
		echo "Year $yr ..."
		set yr_str = ` printf %04d $yr `
		set file_in = ${datapath}/${prefixi}.${yr_str}.nc
		set file_o = tmp.${var}.${yr_str}.nc
		ncks -O -h -v $vars $file_in $file_o
	        echo " $file_in --> $file_o "
		@ yr ++
	end
	echo "Concatenating data ..."
	ncrcat -O -h tmp.$var.* $outfile.nc

# Move data
	echo "Moving data to path: $datato ..."
	if ( ! -e ${datato} ) then
		mkdir ${datato}
	endif
	mv $outfile*.nc ${datato}/
	echo "Removing tmp data ..."
	rm -f tmp.$var.*.nc
	if ( $? == 0) then
		cd ..
		rm -rf ${tmpdir}
		echo "- FINISHED -"
	else
		echo "- NOT FINISHED -"
	endif
