#! /bin/csh
#
# This script get variables from annual mean POP output
#
# Qing Li, 20150713
#

if ( $#argv != 4 ) then
cat << EOF
USAGE:
	cesm_ocean_get_ann [case name] [start year] [end year] [type]
		type = 1: get ocean transport
EOF
exit -1;
endif

set casename	= $1
@ yrini = $2
@ yrend = $3
set type = $4

# set datadir, outdir, workdir
source ./set_env

if ( $type == 1 ) then
	set var = TRANS
	set vars = "MOC,N_HEAT,N_SALT,moc_components,transport_components,transport_regions,moc_z,lat_aux_grid,time"
else
	echo "Unsupported type"
	exit -1
endif

set prefixi	= ${casename}.pop.h
set prefixo	= ${casename}.pop.h
set tmpdir	= $$
set yrini_str	= `printf %04d $yrini`
set yrend_str	= `printf %04d $yrend`
set outfile 	= ${prefixo}.${var}.${yrini_str}.${yrend_str}.ann

echo "- STARTED -"
echo "Input Directory: $datadir"
echo "Output Directory: $outdir"

cd $workdir
mkdir ${tmpdir}
cd ${tmpdir}

echo " Extracting Variable: $var ..."
set yr = $yrini
while ( $yr <= $yrend )
	echo "Year $yr ..."
	set yr_str = ` printf %04d $yr `
	set file_in = ${datadir}/${prefixi}.${yr_str}.nc
	set file_o = tmp.${var}.${yr_str}.nc
	ncks -O -h -v $vars $file_in $file_o
	echo " $file_in --> $file_o "
	@ yr ++
end
echo "Concatenating data ..."
ncrcat -O -h tmp.$var.* $outfile.nc

# Move data
echo "Moving data to path: $outdir ..."
if ( ! -e ${outdir} ) then
	mkdir ${outdir}
endif
mv $outfile*.nc ${outdir}/
echo "Removing tmp data ..."
rm -f tmp.$var.*.nc
if ( $? == 0) then
	cd ..
	rm -rf ${tmpdir}
	echo "- FINISHED -"
else
	echo "- NOT FINISHED -"
endif
