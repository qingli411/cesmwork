#! /bin/csh
#
# This script generate global averaged variables from CCSM ocean output
#
# Qing Li, 20120719
#

if ( $#argv != 4 ) then
cat << EOF
USAGE:
	cesm_ocean_ts [case name] [start year] [end year] [type]
	type	= 1: get temperature
	type	= 2: get salinity
	type	= 3: get mixed layer depth
EOF
exit -1;
endif

set casename	= $1
@ yrini = $2
@ yrend = $3
@ type	= $4

# set datadir, outdir, workdir
source ./set_env

if ( $type == 1) then
	set var		= "GATEMP"
	set vars	= "TEMP"
	set wgt		= "TAREA"
else if ( $type == 2) then
	set var		= "GASALT"
	set vars	= "SALT"
	set wgt		= "TAREA"
else if ( $type == 3) then
	set var		= "GAMLD"
	set vars	= "HMXL_DR,HMXL,HBLT"
	set wgt		= "TAREA"
endif

set prefixi	= ${casename}.pop.h
set prefixo	= ${casename}.pop.h
set yrini_str	= `printf %04d $yrini`
set yrend_str	= `printf %04d $yrend`
set outfile	= ${prefixo}.${var}.${yrini_str}.${yrend_str}.nc
set tmpdir	= $$

echo "- STARTED -"
echo "Input Directory: $datadir"
echo "Output Directory: $outdir"

cd $workdir
mkdir ${tmpdir}
cd ${tmpdir}

echo " Extracting Variable: $var ..."
set yr = $yrini
while ( $yr <= $yrend )
echo "Year $yr ..."
	foreach mon ( 01 02 03 04 05 06 07 08 09 10 11 12 )
		set yr_str = ` printf %04d $yr `
		set file_in = ${datadir}/${prefixi}.${yr_str}-${mon}.nc
		set file_o = tmp.${var}.${yr_str}.${mon}.nc
		ncwa -h -O -w $wgt -a nlat,nlon -v $vars $file_in $file_o
		echo " $file_in --> $file_o "
	end
@ yr ++
end
echo "Concatenating data ..."
ncrcat -O -h tmp.$var.* out.$var.nc

# Move data
echo "Moving data to path: $outdir ..."
if ( ! -e ${outdir} ) then
	mkdir ${outdir}
endif
mv out.${var}.nc ${outdir}/${outfile}
if ( $? == 0) then
	echo "Removing tmp data ..."
	rm -f tmp.$var.*.nc
	cd ..
	rm -rf ${tmpdir}
	echo "- FINISHED -"
else
	echo "- NOT FINISHED -"
endif
